~version: "2.0"

# Find the body, title and subtitle
$article: //article
$title: $article//h1[has-class("titolo")]
$subtitle: $article//p[has-class("box-dati")]
$related_container_art: //div[has-class("contenitore-articoli")]

# Distinguish articles from movie pages
$nav: $article//nav

?path: .+/scheda.*
?path: .+/video.*
?path: .+/episodi/.+

### Movie pages

# Remove nav, as all elements in it are included in the page
# TO CHECK: STREAMING
@remove: $nav

# Remove divider
@remove: $article//hr

# Combine multicolumn lists
@combine: $article//ul[has-class("destra")]

# Remove View All button
@remove: $article//a[has-class("tasto-apri-box")]

# Handle actors
$actors_list: $article//div[has-class("lista-film")]
@combine: $actors_list/div[has-class("row")]
@append(<slideshow>): $actors_list
$actors_slideshow: $actors_list/slideshow
@after(<figure>, "bio", "./@href", "img", "./img/@src", "name", "./div[1]", "dub", "./div[2]"): $actors_list/div/a
@append_to($actors_slideshow): $actors_list/div/figure
@append(<img>, "src", "./@img", "href", "./@bio"): $actors_list/slideshow/figure
@append(<figcaption>, "name", "./@name", "dub", "./@dub"): $actors_list/slideshow/figure
@append(@name): $actors_list/slideshow/figure/figcaption
@append(" - "): $actors_list/slideshow/figure/figcaption
@append(@dub): $actors_list/slideshow/figure/figcaption
@remove: $actors_list/div

# Handle videos and trailers, also images
$trailer_s: $article//section
@prepend_to($trailer_s): $trailer_s//div[has-class("h5") and has-class("descrizione")]
@remove: $trailer_s//div[has-class("h4") and has-class("titolo")]
$other_videos: $trailer_s//a[has-class("videogallery")]
@after(<related>, "myid", "othervideos"): $other_videos
$other_videos_rel: $trailer_s//related[@myid="othervideos"]
@append(<h1>): $other_videos_rel
@append("Vedi gli altri video"): $other_videos_rel/h1
@remove: $other_videos/*
@append_to($other_videos_rel): $other_videos

$gallerybox: $trailer_s//div[has-class("box-galleria-immagini-video")]
@if($gallerybox) {
  @after(<related>, "myid", "gallery"): $gallerybox
  $gallery_rel: $trailer_s//related[@myid="gallery"]
  @append(<h3>): $gallery_rel
  @append("Vedi le foto"): $gallery_rel/h3
  @remove: $gallerybox/a/*
  @append_to($gallery_rel): ($gallerybox/a)[1]
  @replace("#[0-9]", ""): $gallery_rel
}

# Handle star rating
$csrating: $article//div[has-class("voto-comingsoon")]
@append(<div>, "data-rating", ./@data-rating): $csrating
@append(@data-rating): $csrating/div[@data-rating]
@replace("h6", ""): $csrating/@class
@wrap(<p>): $csrating
@remove: $article//div[@id="voto-pubblico"]
@remove: $article//div[@itemprop="aggregateRating"]

# Handle table of contents
$indice: $article//ul[@class="indice"]
@remove: $indice//a[@href="#lascia-un-commento"]/..
@prepend(<h2>): $indice
@append("Indice"): $indice/h2
$anchors: $article//h2[has-class("anchor")]/span
@set_attr("name", @id): $anchors
<anchor>: $anchors

# Weird continua stuff
<div>: //a[@id="next"]/..
<div>: //a[@id="next"]/../..

# Handle episodes
<details>: $article//div[has-class("panel")]
$details: $article//details[has-class("panel")]
@append(<h3>, "localized", "./a/div[1]", "title", "./a/div[2]"): $details
@append(@localized): $details/h3
@append(" - "): $details/h3
@append(@title): $details/h3
@debug: $details/h3

# Videos cleanup
@remove: $trailer_s//div[has-class("box-tags")]
@remove: $trailer_s//div[has-class("box-links")]
@remove: $article//div[@id="voto-pubblico"]
@remove: $article//span[has-class("a-button-shop")]

$body: $article

?path: .+/news/.+
### Articles

# Find basic properties
$author_node: //span[@itemprop="author"]
$author: $author_node//span[@itemprop="name"]/text()
$author_url: $author_node/a/@href
$date_node: //time[@itemprop="datePublished"]
$published_date: $date_node/@datetime
$cover: //figure[@itemprop="image"]
$related_container_mov: //div[has-class("box-film-di-riferimento")]

$body: $article

?true
### Everything

# Create related articles
@append(<related>, "myid", "articles"): $article
$related_articles: $article/related[@myid="articles"]
@append(<h3>): $related_articles
@append("News correlate"): $related_articles/h3
@remove: $related_container_art//a/*
@append_to($related_articles): $related_container_art//a

?path: .+/news/.+
### Back to Articles

@append(<related>, "myid", "movies"): $article
$related_movies: $article/related[@myid="movies"]
@remove: $related_container_mov//a/*
@append_to($related_movies): $related_container_mov//a
@prepend(<h3>): $related_movies
@append("Schede di riferimento"): $related_movies/h3

?true
### Back to Everything

# Cleanup useless elements
@remove: $article//div[has-class("box-dati")]
@remove: $article//div[has-class("tasti-social-top")]
@remove: $article//div[has-class("author")]
@remove: $article//div[has-class("box-share-tasti-sociali")]
@remove: $article//div[has-class("box-tags-articolo")]
@remove: $article//a[has-class("box-titoli-commenti-facebook")]
@remove: $article//div[has-class("box-source-link")]

# Fullsize images
$images: //img
@replace("_ico", ""): $images/@src
@replace("/ico", ""): $images/@src

# Convert titles to the appropriate format
<h1>: //div[has-class("h1")]
<h2>: //div[has-class("h2")]
<h3>: //div[has-class("h3")]
<h4>: //div[has-class("h4")]
<h5>: //div[has-class("h5")]
<h6>: //div[has-class("h6")]

# Test jwplayer support
$jwplayers: $article//iframe[contains(@src, "?idv=")]
@replace(".+/?idv=([0-9]+)(?:&ply=1)?", "https://video.comingsoon.it/MP4/$1.mp4"): $jwplayers/@src
@after(<video>, "src", "./@src"): $jwplayers
@remove: $jwplayers
<div>: //video/ancestor::p

# TEST NESTED FIX
$problems: $article
@while( $problems ) {
  $test: $article//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $article//self::*[prev-sibling::*/@merge = @merge]
  }
}

# Set the appropriate attributes
# Enable/Disable as needed
title: $title
subtitle: $subtitle
# kicker: $kicker
author: $author
author_url: $author_url
published_date: $published_date
# description: $description
# image_url: $image_url
# document_url: $document_url
site_name: "ComingSoon.it"
# channel: "@channelname"
cover: $cover
body: $body