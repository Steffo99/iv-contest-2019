~version: "2.0"

# Find the body, title and subtitle
$article: //article
$title: $article//h1[has-class("titolo")]
$subtitle: $article//p[has-class("box-dati")]
$related_container_art: //div[has-class("contenitore-articoli")]

# Distinguish articles from movie pages
$nav: $article//nav
$nav+: //div[has-class("down-tab-menu")]/nav

?path: .+/scheda.*
?path: .+/video.*
?path: .+/episodi/.+
?path: .+/biografia/
?path: .+/filmografia/
?path: .+/streaming/

### Movie pages

# Combine multicolumn lists
@combine: $article//ul[has-class("destra")]

# Remove View All button
@remove: $article//a[has-class("tasto-apri-box")]

# Remove random headings
@wrap(<p>): $article//div[has-class("dati-biografia")]
@replace("h5", ""): $article//div[has-class("dati-biografia")]/@class

# Handle actors
$actors_list: $article//div[has-class("lista-film")]
@combine: $actors_list/div[has-class("row")]
@append(<slideshow>): $actors_list
$actors_slideshow: $actors_list/slideshow
@after(<figure>, "bio", "./@href", "img", "./img/@src", "name", "./div[1]", "dub", "./div[2]"): $actors_list/div/a
@append_to($actors_slideshow): $actors_list/div/figure
@append(<img>, "src", "./@img", "href", "./@bio"): $actors_list/slideshow/figure
@append(<figcaption>, "name", "./@name", "dub", "./@dub"): $actors_list/slideshow/figure
@append(@name): $actors_list/slideshow/figure/figcaption
@append(" - "): $actors_list/slideshow/figure/figcaption
@append(@dub): $actors_list/slideshow/figure/figcaption
@remove: $actors_list/div

# Handle star rating
$csrating: $article//div[has-class("voto-comingsoon")]
@append(<div>, "data-rating", ./@data-rating): $csrating
@append(@data-rating): $csrating/div[@data-rating]
@replace("h6", ""): $csrating/@class
@wrap(<aside>): $csrating
@append(" / 5 stelle"): $csrating/div[@data-rating]
@remove: $article//div[@id="voto-pubblico"]
@remove: $article//div[has-class("valutazione")]

# Handle table of contents
$indice: $article//ul[@class="indice"]
@remove: $indice//a[@href="#lascia-un-commento"]/..
@prepend(<h2>): $indice
@append("Indice"): $indice/h2
$anchors: $article//h2[has-class("anchor")]/span
@set_attr("name", @id): $anchors
<anchor>: $anchors

# Weird continua stuff
<div>: //a[@id="next"]/..
<div>: //a[@id="next"]/../..

# Handle episodes
<details>: $article//div[has-class("panel")]
$details: $article//details[has-class("panel")]
@append(<summary>, "localized", "./a/div[1]", "title", "./a/div[2]"): $details
@append(@localized): $details/summary
@append(" - "): $details/summary
@append(@title): $details/summary
@remove: $details/a/div

# Handle seasons
$seasons_container: //section/div[has-class("box-stagioni-della-serie")]
@append(<related>, "myid", "seasons"): $article
$seasons: $article/related[@myid="seasons"]
@append_to($seasons): $seasons_container/header/h2
@append_to($seasons): $seasons_container/div/a

# Handle streaming
$streaming: $article//div[has-class("vod_type_title")]
<h3>: $streaming
$streaming: $article//h3[has-class("vod_type_title")]
@append(" "): $article//div[has-class("vod_price")]/span

# Remove divider
@if_not($streaming) {
  @remove: $article//hr
}

# Cleanup
@wrap(<p>): $article//div[has-class("btn-centrato-b")]
@remove: $article//div[@id="voto-pubblico"]
@remove: $article//span[has-class("a-button-shop")]
@remove: $article//div[has-class("contenuto-scheda")]

$body: $article

?path: .+/news/.+
?path: .+/recensione/
#?path: /chi-siamo/
?exists: //div[@id="contenuto-articolo"]
### Articles, Reviews

# Find basic properties
$author_node: //span[@itemprop="author"]
$author: $author_node//span[@itemprop="name"]/text()
$author_url: $author_node/a/@href
$date_node: //time[@itemprop="datePublished"]
$published_date: $date_node/@datetime
$cover: //figure[@itemprop="image"]
$related_container_mov: //div[has-class("box-film-di-riferimento")]

$body: $article

?path: .+/news/
### Related news pages

$article: //article
$related_container_art: //div[has-class("col-xs-12")]/div[has-class("js-masonry")]

$body: $article

?path: .+/foto/
?path: .+/foto/.+
### Photo pages

# Create realboy tag
@prepend(<realbody>): /html
$article: /html/realbody

# Create slideshow
$photo_containers: //div[@itemtype="http://schema.org/ImageObject"]/div[has-class("contenitore-foto")]
$copyinfo: ($photo_containers/../div[has-class("box-dati")]/div[@itemprop="author"])[1]
@debug: $copyinfo
<figure>: $photo_containers
$photo_containers: //div[@itemtype="http://schema.org/ImageObject"]/figure[has-class("contenitore-foto")]
#@append(<figcaption>, "text", "./img/@alt"): $photo_containers
@append(@text): $photo_containers/figcaption
@append_to($article): $nav
@append_to($article): $photo_containers
@append_to($article): $copyinfo

$body: $article

?true
### Everything

# Create related articles
@append(<related>, "myid", "articles"): $article
$related_articles: $article/related[@myid="articles"]
@append(<h3>): $related_articles
@append("News correlate"): $related_articles/h3
@remove: $related_container_art//a/*
@append_to($related_articles): $related_container_art//a

?path: .+/news/
### Related news pages

# Handle seasons
$seasons_container: //section/div[has-class("box-stagioni-della-serie")]
@append(<related>, "myid", "seasons"): $article
$seasons: $article/related[@myid="seasons"]
@append_to($seasons): $seasons_container/header/h2
@append_to($seasons): $seasons_container/div/a

?path: .+/news/.+
?path: .+/recensione/
?path: /chi-siamo/
### Back to Articles and Reviews

@if($related_container_mov//a) {
  @append(<div>, "myid", "movies"): $article
  $related_movies: $article/div[@myid="movies"]
  #@remove: $related_container_mov//a/*
  @append(<h3>): $related_movies
  @append("Schede di riferimento"): $related_movies/h3
  @append_to($related_movies): $related_container_mov//a
  @set_attr("href", "./../@href"): $related_movies//img
}

?true
### Back to Everything

# Handle videos and trailers, also images
#$trailer_s: //section/div[has-class("video-player-xl-articolo")]
$trailer_s: //section[has-class("contenuto-scheda-mob0")]
$trailer_s?: //section[has-class("video-player-xl-contenitore-scheda")]
@prepend_to($trailer_s): $trailer_s//div[has-class("h5") and has-class("descrizione")]
@prepend_to($trailer_s): $trailer_s//div[has-class("h4") and has-class("titolo")]
$other_videos: $trailer_s//a[has-class("videogallery")]
# Display other videos as related
?path: .+/video/.+
@after(<related>, "myid", "othervideos"): $other_videos
$other_videos_rel: $trailer_s//related[@myid="othervideos"]
@append(<h1>): $other_videos_rel
@append("Vedi gli altri video"): $other_videos_rel/h1
@remove: $other_videos/*
@append_to($other_videos_rel): $other_videos
?path_not: .+/video/.+
@remove: $other_videos
?true

$gallerybox: $trailer_s/div[has-class("box-galleria-immagini-video")]
@if($gallerybox) {
  @after(<div>, "myid", "gallery"): $gallerybox
  $gallerylink: $trailer_s//div[@myid="gallery"]
  @remove: $gallerybox/a/*
  @append_to($gallerylink): ($gallerybox/a)[1]
  @append("VEDI TUTTE LE FOTO"): $gallerylink/a
  @wrap(<p>): $gallerylink
}
@remove: $trailer_s//div[has-class("box-tags")]
$jwplayers: $article//iframe[contains(@src, "?idv=")]
@replace(".+/?idv=([0-9]+)(?:&ply=1)?", "https://video.comingsoon.it/MP4/$1.mp4"): $jwplayers/@src
@after(<video>, "src", "./@src"): $jwplayers
@remove: $jwplayers
<div>: //video/ancestor::p

# Handle nav
@remove: $nav/ul/li[has-class("active")]
@remove: $nav/ul/li[has-class("disabled")]
@remove: $nav/ul/li[a[@id="fb-comment-link-goto-button"]]

# Cleanup useless elements
@remove: $article//div[has-class("box-tags")]
@remove: $article//div[has-class("box-dati")]
@remove: $article//div[has-class("tasti-social-top")]
@remove: $article//div[has-class("author")]
@remove: $article//div[has-class("box-share-tasti-sociali")]
@remove: $article//div[has-class("box-tags-articolo")]
@remove: $article//a[has-class("box-titoli-commenti-facebook")]
@remove: $article//div[has-class("box-source-link")]
@remove: $article//select
@remove: $article//a[has-class("disabled")]
@remove: $article//li[not(normalize-space())]

# Mark marked elements
@wrap_inner(<b>): //li[has-class("evidenziato-in-box")]

# Fullsize images
$images: //img
@replace("_ico", ""): $images[not(contains(@src, "def_ico")) and not(contains(@src, "_icov"))]/@src
@replace("/ico", ""): $images/@src

# Convert titles to the appropriate format
<h1>: //div[has-class("h1")]
<h2>: //div[has-class("h2")]
<h3>: //div[has-class("h3")]
<h4>: //div[has-class("h4")]
<h5>: //div[has-class("h5")]
<h6>: //div[has-class("h6")]

# Add image links
$links_with_image: $article//img/ancestor::a
@set_attr("href", "./ancestor::a/@href"): $links_with_image//img

# TEST NESTED FIX
$problems: $article
@while( $problems ) {
  $test: $article//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $article//self::*[prev-sibling::*/@merge = @merge]
  }
}

# Set the appropriate attributes
# Enable/Disable as needed
title: $title
subtitle: $subtitle
# kicker: $kicker
author: $author
author_url: $author_url
published_date: $published_date
# description: $description
# image_url: $image_url
# document_url: $document_url
site_name: "ComingSoon.it"
# channel: "@channelname"
cover: $cover
body: $body

@if_not( $body ) {
  image_url: ""
  document_url: ""
}
