~version: "2.0"

# Find the body, title and subtitle
$body: //article
$title: $body//h1[has-class("titolo")]
$subtitle: $body//p[has-class("box-dati")]
$related_container_art: //div[has-class("contenitore-articoli")]

# Distinguish articles from movie pages
$nav: $body//nav

?exists: $nav
### Movie pages

# Remove nav, as all elements in it are included in the page
# TO CHECK: FOTO, RECENSIONE, STREAMING
@remove: $nav

# Remove divider
@remove: $body//hr

# Combine multicolumn lists
@combine: $body//ul[has-class("destra")]

# Remove View All button
@remove: $body//a[has-class("tasto-apri-box")]

# Handle actors
$actors_list: $body//div[has-class("lista-film")]
@combine: $actors_list/div[has-class("row")]
@append(<slideshow>): $actors_list
$actors_slideshow: $actors_list/slideshow
@after(<figure>, "bio", "./@href", "img", "./img/@src", "name", "./div[1]", "dub", "./div[2]"): $actors_list/div/a
@append_to($actors_slideshow): $actors_list/div/figure
@append(<img>, "src", "./@img", "href", "./@bio"): $actors_list/slideshow/figure
@append(<figcaption>, "name", "./@name", "dub", "./@dub"): $actors_list/slideshow/figure
@append(@name): $actors_list/slideshow/figure/figcaption
@append(" - "): $actors_list/slideshow/figure/figcaption
@append(@dub): $actors_list/slideshow/figure/figcaption
@remove: $actors_list/div

# Handle videos and trailers
$trailer_s: $body//section
@prepend_to($trailer_s): $trailer_s//div[has-class("h5") and has-class("descrizione")]
#@prepend_to($trailer_s): $trailer_s//div[has-class("h4") and has-class("titolo")]
$other_videos: $trailer_s//a[has-class("videogallery")]
@map($other_videos) {
  $video: $@
  @replace("^/", "https://www.comingsoon.it/film/"): $video/@href
  @append(<iframe>, "src", "./@href"): $video
  #@append_to($trailer_s): $video//div[has-class("titolo")]
  @append_to($trailer_s): $video//div[has-class("descrizione")]
  @append_to($trailer_s): $video/iframe
  @remove: $video
}

# Videos cleanup
@remove: $trailer_s//div[has-class("box-tags")]
@remove: $trailer_s//div[has-class("box-links")]

?not_exists: $nav
### Articles

# Find basic properties
$author_node: //span[@itemprop="author"]
$author: $author_node//span[@itemprop="name"]/text()
$author_url: $author_node/a/@href
$date_node: //time[@itemprop="datePublished"]
$published_date: $date_node/@datetime
$cover: //figure[@itemprop="image"]
$related_container_mov: //div[has-class("box-film-di-riferimento")]

?true

# Create related articles
@append(<related>, "myid", "articles"): $body
$related_articles: $body/related[@myid="articles"]
@remove: $related_container_art//a/*
@append_to($related_articles): $related_container_art//a

# Create related movies
@if_not($nav) {
  @append(<related>, "myid", "movies"): $body
  $related_movies: $body/related[@myid="movies"]
  @remove: $related_container_mov//a/*
  @append_to($related_movies): $related_container_mov//a
  @prepend(<h3>): $related_movies
  @append("Schede di riferimento"): $related_movies/h3
}

# Cleanup useless elements
@remove: $body//div[has-class("box-dati")]
@remove: $body//div[has-class("tasti-social-top")]
@remove: $body//div[has-class("author")]
@remove: $body//div[has-class("box-share-tasti-sociali")]
@remove: $body//div[has-class("box-tags-articolo")]
@remove: $body//a[has-class("box-titoli-commenti-facebook")]
@remove: $body//div[has-class("box-source-link")]

# Convert titles to the appropriate format
<h1>: //div[has-class("h1")]
<h2>: //div[has-class("h2")]
<h3>: //div[has-class("h3")]
<h4>: //div[has-class("h4")]
<h5>: //div[has-class("h5")]
<h6>: //div[has-class("h6")]

# Test jwplayer support
$jwplayers: $body//iframe[contains(@src, "?idv=")]
$jwplayers+: $body//iframe[contains(@src, "?vid=")]
@replace(".+/?(?:idv|vid)=([0-9]+)", "https://video.comingsoon.it/MP4/$1.mp4"): $jwplayers/@src
@after(<video>, "src", "./@src"): $jwplayers
@remove: $jwplayers
<div>: //video/ancestor::p

# TEST NESTED FIX
$problems: $body
@while( $problems ) {
  $test: $body//*[self::blockquote or self::aside or self::figure or (self::img and not(./ancestor::figure)) or self::iframe or self::slideshow or self::related or self::footer or self::table[not(ancestor::table) and not(descendant::table)]]
 
  $problems: $test/self::*[ancestor::anchor or ancestor::blockquote or ancestor::aside or ancestor::footer or ancestor::strong or ancestor::b or ancestor::em or ancestor::i or ancestor::ins or ancestor::u or ancestor::del or ancestor::s or ancestor::strike or ancestor::code or ancestor::kbd or ancestor::samp or ancestor::tt or ancestor::mark or ancestor::sup or ancestor::pic or ancestor::sub or ancestor::a or ancestor::reference or ancestor::h1 or ancestor::h2 or ancestor::h3 or ancestor::h4 or ancestor::h5 or ancestor::h6 or ancestor::p or ancestor::pre or ancestor::hr or ancestor::img or ancestor::br]
 
  @if( $problems ) {
    $parent: $problems/parent::*
    @detach: $parent/node()
    @set_attr("merge", $index): $@
    <remove>: $parent
    @after_el("./parent::*"): $problems
    @combine: $body//self::*[prev-sibling::*/@merge = @merge]
  }
}

# Set the appropriate attributes
# Enable/Disable as needed
title: $title
subtitle: $subtitle
# kicker: $kicker
author: $author
author_url: $author_url
published_date: $published_date
# description: $description
# image_url: $image_url
# document_url: $document_url
site_name: "ComingSoon.it"
# channel: "@channelname"
cover: $cover
body: $body
